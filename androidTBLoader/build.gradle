
// External dependencies for the build script. (Gradle, WTF...)
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

project(':core') {          
    buildDir = 'android_build'
}

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        maven {
            url "http://bits.netbeans.org/maven2"
        }
        maven {
            url "http://essi-lab.eu/nexus/content/groups/open.repos"
        }
    }
}



apply plugin: 'com.android.application'

// Load Google Play Store signing properties
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    // Load versionCode from version.properties file.
    def Properties versionProps = new Properties()
    def versionPropsFile = file('version.properties')
    if(versionPropsFile.exists())
        versionProps.load(new FileInputStream(versionPropsFile))
    def code = (versionProps['versionCode'] ?: "0").toInteger()

    // If this is a release build (ie, will be signed for the play store), increment versionCode, and save.
    def runTasks = gradle.startParameter.taskNames
    for (String item : runTasks) {
        if (item.contains("assembleRelease")) {
            code = code + 1
            versionProps['versionCode'] = code.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        }
    }

    signingConfigs {
        Release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 23
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "org.literacybridge.androidtbloader"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode code
        versionName "0.9.5 (${code})"
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.Release
        }
    }
}

clean.doLast  {
    delete rootProject.buildDir
    delete project(':core').buildDir
}

dependencies {
    compile project(':core')
    
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    //compile files('libs/dropbox-android-sdk-1.6.3.jar')
    compile files('libs/json_simple-1.1.jar')
    compile 'com.amazonaws:aws-android-sdk-s3:2.2.+'
    compile 'com.amazonaws:aws-android-sdk-cognitoidentityprovider:2.3.2'

    testCompile 'junit:junit:4.12'
}
